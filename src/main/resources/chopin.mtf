@time 3/4
@tempo 120
@key A minor

Am = [-a c e]
Dm = [-a d f]
G7 = [-g -b f]
G7d = [-g d f]
Cg = [-g c e]
E7 = [-e -b d]
E7g = [-e -g# d]
AmE = [-e -a c]

theme = {-(a b) c c (d e) f* (-b c d a g f)*. (((e f) e) d#) e*}
chords = {~ Am Am ~ Dm Dm ~ G7 G7 ~ Cg Cg}

A = {
    treble = +{
        theme -(a b) c c (d e) f* (-b c d a g -b)*. c ~ -e
        theme (c d) e e (f g) a* g (f# g d f)* e*.
    }
    chords = {chords ~ Am Am ~ Dm Dm ~ G7 G7d ~ Cg ~ chords chords}
    bass = {-3a ~* -2d ~* -2g ~* -c ~* -2a ~* -2d ~* -2g ~* -c ~*}
    e [treble chords {bass bass}]
}

B = {
    treble = +{
        (e f# g# a b +c)*.
        (((b +c) b) a e b)* a
        (((a b) a) g# e f)* e
        (((e f) e) c -a -b)* -a
        (-e -g# -b) (e g# b +e +g#) +{(b. c)
        (((b +c) b) a e b)* a
        (((a b) a) g# e +e)* +e a* ~}
    }
    bass = {-2e E7 E7g -2a AmE AmE -2e E7 E7 -2a AmE AmE}
    [treble {bass bass}]
}

C = {
    A = [-e a c#]
    B7 = [-a -b d#]
    treble1 = +{theme (c d) e e (f g) a* (d# e) f# f# (g# a) b*}
    fragment = +(-b c# d f# e d (c# d c#) -b c# -g# -a -f#)*2.
    treble2 = +{
        fragment (((e f#) e) d -g# f#)* e (((e f#) e) c# -a f#)* e
        fragment (((e f#) e) d -g# f# e -g#)*. -a ~ -e
    }
    bass1 = [{chords ~ Am Am ~ Dm Dm ~ B7 B7 ~ E7 ~} {-3a ~* -2d ~* -2g ~* -c ~* -2a ~* -2d ~* -3b ~* -2e ~*}]
    bass2 = {-2e E7g E7g -2a A A -2e E7 E7 -2a A A -2e E7g E7g -2a A A -2e E7 E7 -2a [-e c#] ~}
    [treble1 bass1] [treble2 bass2]
}

coda = {
    treble = +{
        theme -(a b) c c (d e) f* (-b c d a g -b)*. c ~ -e
        -(a b) c c (d e) f* ~' (e f e)' (d# e b d)* c* ~
        (~ +c b a g f e d c# d e f)*2. (f e f e f e) (d# e f -g#)* -a* ~
    }
    chords = {
        chords
        ~ Am Am ~ Dm Dm ~ G7 G7d ~ [-g e] ~
        ~ Am Am ~ Dm Dm ~ E7g E7g ~ Cg Cg
        ~ [-e -a e] ~* [-a -b d] ~* [-e c] E7 ~ [-e c] ~
    }
    bass = {
        -3a ~* -2d ~* -2g ~* -c ~* -2a ~* -2d ~* -2g ~* -c ~*
        -3a ~* -2d ~* -2e ~* -2a ~* -c ~* -d ~* -2e ~* -2a ~*
    }
    [treble chords bass]
}

A B B C C coda
